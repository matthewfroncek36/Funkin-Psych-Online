package backend;

import haxe.io.Path;
import flixel.graphics.frames.FlxFramesCollection;
import openfl.utils.Future;
import flixel.system.FlxAssets.FlxGraphicAsset;
import flxanimate.data.SpriteMapData.FlxSpriteMap;
import flxanimate.frames.FlxAnimateFrames;
import flixel.graphics.frames.FlxFrame.FlxFrameAngle;
import flixel.graphics.frames.FlxAtlasFrames;
import flixel.graphics.FlxGraphic;
import flixel.math.FlxPoint;
import flixel.math.FlxRect;

import openfl.display.BitmapData;
import openfl.display3D.textures.RectangleTexture;
import openfl.utils.AssetType;
import openfl.utils.Assets as OpenFlAssets;
import openfl.system.System;
import openfl.geom.Rectangle;

import lime.utils.Assets;
import openfl.media.Sound;

#if sys
import sys.io.File;
import sys.FileSystem;
#if linux
import haxe.io.Path;
#end
#end
import tjson.TJSON as Json;

#if MODS_ALLOWED
import backend.Mods;
#end

class Paths
{
	inline public static var SOUND_EXT = #if web "mp3" #else "ogg" #end;
	inline public static var VIDEO_EXT = "mp4";
	inline public static var PATH_SLASH = #if windows '\\' #else '/' #end;

	public static function excludeAsset(key:String) {
		if (!dumpExclusions.contains(key))
			dumpExclusions.push(key);
	}

	public static var dumpExclusions:Array<String> =
	[
		'assets/music/freakyMenu.$SOUND_EXT',
		'assets/shared/music/breakfast.$SOUND_EXT',
		'assets/shared/music/tea-time.$SOUND_EXT',
		'assets/images/bf1.png',
		'assets/images/bf2.png',
	];
	/// haya I love you for the base cache dump I took to the max
	public static function clearUnusedMemory() {
		// clear non local assets in the tracked assets list
		for (key in currentTrackedAssets.keys()) {
			// if it is not currently contained within the used local assets
			if (!localTrackedAssets.contains(key) && !dumpExclusions.contains(key)) {
				var obj = currentTrackedAssets.get(key);
				@:privateAccess
				if (obj != null) {
					// remove the key from all cache maps
					FlxG.bitmap._cache.remove(key);
					openfl.Assets.cache.removeBitmapData(key);
					currentTrackedAssets.remove(key);

					// and get rid of the object
					obj.persist = false; // make sure the garbage collector actually clears it up
					obj.destroyOnNoUse = true;
					obj.destroy();
				}
			}
		}

		// run the garbage collector for good measure lmfao
		System.gc();
	}

	// define the locally tracked assets
	public static var localTrackedAssets:Array<String> = [];
	public static function clearStoredMemory(?cleanUnused:Bool = false) {
		// clear anything not in the tracked assets list
		@:privateAccess
		for (key => obj in FlxG.bitmap._cache)
		{
			if (obj != null && !currentTrackedAssets.exists(key) && !dumpExclusions.contains(key)) {
				openfl.Assets.cache.removeBitmapData(key);
				FlxG.bitmap._cache.remove(key);
				// pointer not found?
				try {
					obj.destroy();
				} catch (exc) {
					trace(exc);
				}
			}
		}

		// clear all sounds that are cached
		for (key in currentTrackedSounds.keys()) {
			if (!localTrackedAssets.contains(key) && !dumpExclusions.contains(key) && key != null) {
				//trace('test: ' + dumpExclusions, key);
				Assets.cache.clear(key);
				currentTrackedSounds.remove(key);
			}
		}
		sparrowAtlasCache.clear();
		packerAtlasCache.clear();
		// flags everything to be cleared out next unused memory clear
		localTrackedAssets = [];
		#if !html5 openfl.Assets.cache.clear("songs"); #end
	}

	static public var currentLevel:String = 'week1';
	static public function setCurrentLevel(name:String)
	{
		currentLevel = name.toLowerCase();
	}

	public static function getPath(file:String, ?type:AssetType = TEXT, ?library:Null<String> = null, ?modsAllowed:Bool = false, ?modDir:String):String
	{
		#if MODS_ALLOWED
		if(modsAllowed)
		{
			var modded:String = modFolders(file, modDir);
			if(FileSystem.exists(modded)) return modded;
		}
		#end

		if (library != null)
			return getLibraryPath(file, library);

		if (currentLevel != null)
		{
			var levelPath:String = '';
			if(currentLevel != 'shared') {
				levelPath = getLibraryPathForce(file, 'week_assets', currentLevel);
				if (OpenFlAssets.exists(levelPath, type))
					return levelPath;
			}

			levelPath = getLibraryPathForce(file, "shared");
			if (OpenFlAssets.exists(levelPath, type))
				return levelPath;
		}

		return getPreloadPath(file);
	}

	static public function getLibraryPath(file:String, library = "preload")
	{
		return if (library == "preload" || library == "default") getPreloadPath(file); else getLibraryPathForce(file, library);
	}

	inline public static function getLibraryPathForce(file:String, library:String, ?level:String)
	{
		if(level == null) level = library;
		var returnPath = '$library:assets/$level/$file';
		return returnPath;
	}

	inline public static function getPreloadPath(file:String = '')
	{
		return 'assets/$file';
	}

	inline static public function getFolderPath(file:String, folder = "shared")
		return 'assets/$folder/$file';

	inline public static function getSharedPath(file:String = '')
		return getFolderPath(file);

	inline static public function txt(key:String, ?library:String)
	{
		return getPath('data/$key.txt', TEXT, library);
	}

	inline static public function xml(key:String, ?library:String)
	{
		return getPath('data/$key.xml', TEXT, library);
	}

	inline static public function json(key:String, ?library:String)
	{
		return getPath('data/$key.json', TEXT, library);
	}

	inline static public function shaderFragment(key:String, ?library:String)
	{
		return getPath('shaders/$key.frag', TEXT, library);
	}
	inline static public function shaderVertex(key:String, ?library:String)
	{
		return getPath('shaders/$key.vert', TEXT, library);
	}
	inline static public function lua(key:String, ?library:String)
	{
		return getPath('$key.lua', TEXT, library);
	}

	static public function video(key:String)
	{
		#if MODS_ALLOWED
		var file:String = modsVideo(key);
		if(FileSystem.exists(file)) {
			return file;
		}
		#end
		return 'assets/videos/$key.$VIDEO_EXT';
	}

	static public function sound(key:String, ?library:String):Sound
	{
		var sound:Sound = returnSound('sounds', key, library);
		return sound;
	}

	inline static public function soundRandom(key:String, min:Int, max:Int, ?library:String)
	{
		return sound(key + FlxG.random.int(min, max), library);
	}

	inline static public function music(key:String, ?library:String):Sound
	{
		var file:Sound = returnSound('music', key, library);
		return file;
	}

	inline static public function returnSoundShit(path:String, ?postfix:String)
	{
		var songKey:String = path;
		if(postfix != null) songKey += '-' + postfix;
		return returnSound('songs', songKey);
	}

	inline static public function voices(song:String, postfix:String = null, songSuffix:String = null):Any {
		var diff = Difficulty.getString().toLowerCase();

		var voices = returnSoundShit('${formatToSongPath(song)}/Voices' + (songSuffix ?? ""), postfix);
		var voicesDiff = returnSoundShit('${formatToSongPath(song)}/Voices-$diff', postfix);

		var voicesCNE = returnSoundShit('${formatToSongPath(song)}/song/Voices' + (songSuffix ?? ""), postfix);
		var voicesDiffCNE = returnSoundShit('${formatToSongPath(song)}/song/Voices-$diff', postfix);

		if (voicesDiffCNE != null) return voicesDiffCNE;
		else if (voicesCNE != null) return voicesCNE;
		else if (voicesDiff != null) return voicesDiff;
		else return voices;

		/*
		var voices:Sound = null;
		try {
			var songKey:String = '${formatToSongPath(song)}/Voices' + (songSuffix ?? "");
			if (postfix != null)
				songKey += '-' + postfix;
			var sound = returnSound('songs', songKey);
			if (sound == null || sound.length <= 0)
				sound = null;
			voices = sound;
		}
		catch (_) {
			voices = null;
		}

		return voices;
		*/
	}

	inline static public function inst(song:String, songSuffix:String = null):Any
	{
		#if html5
		return 'songs:assets/songs/${formatToSongPath(song)}/Inst.$SOUND_EXT';
		#else
		var diff = Difficulty.getString().toLowerCase();

		var inst = returnSoundShit('${formatToSongPath(song)}/Inst' + (songSuffix ?? ""), null);
		var instDiff = returnSoundShit('${formatToSongPath(song)}/Inst-$diff', null);

		var instCNE = returnSoundShit('${formatToSongPath(song)}/song/Inst' + (songSuffix ?? ""), null);
		var instDiffCNE = returnSoundShit('${formatToSongPath(song)}/song/Inst-$diff', null);

		if (instDiffCNE != null) return instDiffCNE;
		else if (instCNE != null) return instCNE;
		else if (instDiff != null) return instDiff;
		else return inst;
		#end
	}

	static var lastImageErrorFile:String = null;

	public static var currentTrackedAssets:Map<String, FlxGraphic> = [];
	static public function image(key:String, ?library:String = null, ?allowGPU:Bool = true, ?isGlobalPath:Bool = false, ?disablePathSystem:Bool):FlxGraphic
	{
		var bitmap:BitmapData = null;
		var file:String = null;
		if (disablePathSystem) file = key; //silly me.

		#if MODS_ALLOWED
		if (!disablePathSystem) {
			if (isGlobalPath) {
				file = modFolders(key + '.png');
				if (FunkinFileSystem.exists(modFolders(key + '_${ClientPrefs.data.lang}.png'))) {
					file = modFolders(key + '_${ClientPrefs.data.lang}.png');
				}
			}
			else {
				file = modsImages(key);
				if (FunkinFileSystem.exists(modsImages(key + '_${ClientPrefs.data.lang}'))) {
					file = modsImages(key + '_${ClientPrefs.data.lang}');
				}
			}
		}
		//trace(file);
		if (currentTrackedAssets.exists(file))
		{
			localTrackedAssets.push(file);
			return currentTrackedAssets.get(file);
		}
		else if (FunkinFileSystem.exists(file))
			bitmap = FunkinFileSystem.getBitmapData(file);
		else if (currentTrackedAssets.exists(file))
		{
			localTrackedAssets.push(file);
			return currentTrackedAssets.get(file);
		}
		else if (FunkinFileSystem.exists(file))
			bitmap = FunkinFileSystem.getBitmapData(file);
		else
		#end
		{
			if (!disablePathSystem) {
				if (isGlobalPath) {
					file = getPath('$key.png', IMAGE, library);
					if (FunkinFileSystem.exists(getPath('${key}_${ClientPrefs.data.lang}.png', IMAGE, library))) {
						file = getPath('${key}_${ClientPrefs.data.lang}.png', IMAGE, library);
					}
				} else {
					file = getPath('images/$key.png', IMAGE, library);
					if (FunkinFileSystem.exists(getPath('images/${key}_${ClientPrefs.data.lang}.png', IMAGE, library))) {
						file = getPath('images/${key}_${ClientPrefs.data.lang}.png', IMAGE, library);
					}
				}
			}
			//trace(file);
			if (currentTrackedAssets.exists(file))
			{
				localTrackedAssets.push(file);
				return currentTrackedAssets.get(file);
			}
			else if (OpenFlAssets.exists(file, IMAGE))
				bitmap = OpenFlAssets.getBitmapData(file);
		}

		if (bitmap != null) {
			return bitmapToGraphic(file, bitmap);
		}

		//STOP FUCKING USING TRACE ITS CPU HEAVY
		if (lastImageErrorFile != file && ClientPrefs.isDebug()) {
			Sys.println('Paths.image(): oh no its returning null NOOOO ($file)');
			lastImageErrorFile = file;
		}
		return null;
	}

	/**
		use if you know how threads work
		note: flxgraphic is not possible to obtain asynchronically, use bitmapToGraphic() in the main thread after fetching the bitmap
	**/
	static public function asyncBitmap(key:String, ?library:String = null, ?modDir:String):Null<Future<BitmapData>> {
		var file:String = null;

		#if MODS_ALLOWED
		file = modsImages(key, modDir);
		// return cached
		if (currentTrackedAssets.exists(file))
		{
			localTrackedAssets.push(file);
			return Future.withValue(currentTrackedAssets.get(file).bitmap);
		}
		// found in the mods files
		else if (FileSystem.exists(file))
			return BitmapData.loadFromFile(file);
		// load from assets
		else
		#end
		{
			file = getPath('images/$key.png', IMAGE, library);
			if (currentTrackedAssets.exists(file))
			{
				localTrackedAssets.push(file);
				return Future.withValue(currentTrackedAssets.get(file).bitmap);
			}
			else if (OpenFlAssets.exists(file, IMAGE))
				return OpenFlAssets.loadBitmapData(file);
		}

		//STOP FUCKING USING TRACE ITS CPU HEAVY
		if (lastImageErrorFile != file && ClientPrefs.isDebug()) {
			Sys.println('Paths.asyncBitmap(): oh no its returning null NOOOO ($file)');
			lastImageErrorFile = file;
		}
		return null;
	}

	static public function bitmapToGraphic(file:String, bitmap:BitmapData) {
		localTrackedAssets.push(file);
		// if (allowGPU /*&& ClientPrefs.data.cacheOnGPU*/)
		// {
		// 	var texture:RectangleTexture = FlxG.stage.context3D.createRectangleTexture(bitmap.width, bitmap.height, BGRA, true);
		// 	texture.uploadFromBitmapData(bitmap);
		// 	bitmap.image.data = null;
		// 	bitmap.dispose();
		// 	bitmap.disposeImage();
		// 	bitmap = BitmapData.fromTexture(texture);
		// }
		var newGraphic:FlxGraphic = FlxGraphic.fromBitmapData(bitmap, false, file);
		newGraphic.persist = true;
		newGraphic.destroyOnNoUse = false;
		currentTrackedAssets.set(file, newGraphic);
		return newGraphic;
	}

	static public function getTextFromFile(key:String, ?ignoreMods:Bool = false):String
	{
		#if sys
		#if MODS_ALLOWED
		if (!ignoreMods && FileSystem.exists(modFolders(key)))
			return File.getContent(modFolders(key));
		#end

		if (FunkinFileSystem.exists(getPreloadPath(key)))
			return FunkinFileSystem.getText(getPreloadPath(key));

		if (currentLevel != null)
		{
			var levelPath:String = '';
			if(currentLevel != 'shared') {
				levelPath = getLibraryPathForce(key, 'week_assets', currentLevel);
				if (FunkinFileSystem.exists(levelPath))
					return FunkinFileSystem.getText(levelPath);
			}

			levelPath = getLibraryPathForce(key, 'shared');
			if (FunkinFileSystem.exists(levelPath))
				return FunkinFileSystem.getText(levelPath);
		}
		#end
		var path:String = getPath(key, TEXT);
		if(OpenFlAssets.exists(path, TEXT)) return Assets.getText(path);
		return null;
	}

	inline static public function font(key:String)
	{
		#if MODS_ALLOWED
		var file:String = modsFont(key);
		if(FileSystem.exists(file)) {
			return file;
		}
		#end
		return 'assets/fonts/$key';
	}

	public static function fileExists(key:String, type:AssetType, ?ignoreMods:Bool = false, ?library:String = null)
	{
		#if MODS_ALLOWED
		if(!ignoreMods)
		{
			for(mod in Mods.getGlobalMods())
				if (FileSystem.exists(mods('$mod/$key')))
					return true;

			if (FileSystem.exists(mods(Mods.currentModDirectory + '/' + key)) || FileSystem.exists(mods(key)))
				return true;
		}
		#end

		if(OpenFlAssets.exists(getPath(key, type, library, false))) {
			return true;
		}
		return false;
	}

	// less optimized but automatic handling
	static public function getAtlas(key:String, ?library:String = null):FlxAtlasFrames
	{
		#if MODS_ALLOWED
		if(FileSystem.exists(modsXml(key)) || OpenFlAssets.exists(getPath('images/$key.xml', library), TEXT))
		#else
		if(OpenFlAssets.exists(getPath('images/$key.xml', library)))
		#end
		{
			return getSparrowAtlas(key, library);
		}
		return getPackerAtlas(key, library);
	}

	static var sparrowAtlasCache:Map<String, FlxAtlasFrames> = new Map();
	inline static public function getSparrowAtlas(key:String, ?library:String = null, ?allowGPU:Bool = true):FlxAtlasFrames
	{
		if (sparrowAtlasCache.exists(key + library))
			return sparrowAtlasCache.get(key + library);

		#if MODS_ALLOWED
		var imageLoaded:FlxGraphic = image(key, allowGPU);
		var xmlExists:Bool = false;
		var xmlLangExists:Bool = false;

		var xml:String = modsXml(key);
		var xmlLang:String = modsXml(key + '_${ClientPrefs.data.lang}');
		if(FileSystem.exists(xml)) {
			xmlExists = true;
		}
		if(FileSystem.exists(xmlLang)) {
			xmlLangExists = true;
		}
		if (xmlExists)
			xml = File.getContent(xml);
		else if (xmlLangExists)
			xml = File.getContent(xmlLang);
		else if (FunkinFileSystem.exists(getPath('images/${key}_${ClientPrefs.data.lang}.xml', library)))
			xml = getPath('images/${key}_${ClientPrefs.data.lang}.xml', library);
		else
			xml = getPath('images/$key.xml', library);

		var frames = FlxAtlasFrames.fromSparrow((imageLoaded != null ? imageLoaded : image(key, library, allowGPU)), xml);
		#else
		var xml:String = getPath('images/$key.xml', library);
		var xmlLang:String = getPath('images/${key}_${ClientPrefs.data.lang}.xml', library);
		if (FunkinFileSystem.exists(xmlLang)) xml = xmlLang;

		var frames = FlxAtlasFrames.fromSparrow(image(key, library, allowGPU), xml);
		#end

		if (frames != null)
			sparrowAtlasCache.set(key + library, frames);

		return frames;
	}

	static var packerAtlasCache:Map<String, FlxAtlasFrames> = new Map();
	inline static public function getPackerAtlas(key:String, ?library:String = null, ?allowGPU:Bool = true):FlxAtlasFrames
	{
		if (packerAtlasCache.exists(key + library))
			return packerAtlasCache.get(key + library);

		#if MODS_ALLOWED
		var imageLoaded:FlxGraphic = image(key, allowGPU);
		var txtExists:Bool = false;
		
		var txt:String = modsTxt(key);
		if(FunkinFileSystem.exists(txt)) {
			txtExists = true;
		}

		var frames = FlxAtlasFrames.fromSpriteSheetPacker((imageLoaded != null ? imageLoaded : image(key, library, allowGPU)), (txtExists ? FunkinFileSystem.getText(txt) : getPath('images/$key.txt', library)));
		#else
		var frames = FlxAtlasFrames.fromSpriteSheetPacker(image(key, library, allowGPU), getPath('images/$key.txt', library));
		#end

		if (frames != null)
			packerAtlasCache.set(key + library, frames);

		return frames;
	}

	inline static public function script(key:String, ?library:String, ?onlyGlobals:Bool) {
		#if (SCRIPTING_ALLOWED || HSC_ALLOWED)
		var scriptToLoad:String = null;
		for(ex in ["hsc", "porno", "class", "script", "pex"]) {
			#if MODS_ALLOWED
			if (onlyGlobals) {
				for(mod in Mods.getGlobalMods()) {
					if (FunkinFileSystem.exists(mods('$key.$ex')))
						scriptToLoad = mods('$key.$ex');
				}
			} else {
				scriptToLoad = Paths.modFolders('$key.$ex');
			}
			if(!FunkinFileSystem.exists(scriptToLoad))
				scriptToLoad = 'assets/$key';
			#else
			scriptToLoad = 'assets/$key';
			#end

			if(FunkinFileSystem.exists(scriptToLoad))
				break;
		}
		return scriptToLoad;
		#end
	}

	static public function getFolderContent(key:String, addPath:Bool = false, source:String = "BOTH"):Array<String> {
		var content:Array<String> = [];
		var folder = key.endsWith('/') ? key : key + '/';

		#if MODS_ALLOWED
		if (source == "MODS" || source == "BOTH") {
			var modDirs:Array<String> = [];
			if (Mods.currentModDirectory != null && Mods.currentModDirectory.length > 0)
				modDirs.push(Mods.currentModDirectory);
			modDirs = modDirs.concat(Mods.getGlobalMods());

			for (mod in modDirs) {
				var modFolder = mods('$mod/$folder');
				if (FileSystem.exists(modFolder)) {
					for (file in FileSystem.readDirectory(modFolder)) {
						if (!FileSystem.isDirectory('$modFolder/$file')) {
							var path = addPath ? '$folder$file' : file;
							if (!content.contains(path))
								content.push(path);
						}
					}
				}
			}
		}
		#end

		if (content != []) return content;
		trace('Content not found');
		return null;
	}

	static var invalidChars = ~/[~&\\;:<>#]/;
	static var hideChars = ~/[.,'"'%?!]/;

	inline static public function formatToSongPath(path:String) {
		var path = invalidChars.split(path.replace(' ', '-')).join("-");
		return hideChars.split(path).join("").toLowerCase();
	}

	public static var currentTrackedSounds:Map<String, Sound> = [];
	public static function returnSound(path:String, key:String, ?library:String) {
		#if MODS_ALLOWED
		var file:String = modsSounds(path, key);
		if(FunkinFileSystem.exists(file)) {
			try {
				if(!currentTrackedSounds.exists(file)) {
					currentTrackedSounds.set(file, FunkinFileSystem.getSound(file));
				}
			} catch (e:Dynamic) {
				if (ClientPrefs.isDebug())
					Sys.println('Paths.returnSound(): SOUND NOT FOUND: $key');
				CoolUtil.showPopUp('SOUND NOT FOUND: $key', 'Paths.returnSound():');
				return null;
			}
			localTrackedAssets.push(key);
			return currentTrackedSounds.get(file);
		}
		#end
		// I hate this so god damn much
		var gottenPath:String = getPath('$path/$key.$SOUND_EXT', SOUND, library);
		gottenPath = gottenPath.substring(gottenPath.indexOf(':') + 1, gottenPath.length);
		// trace(gottenPath);
		try {
			if(!currentTrackedSounds.exists(gottenPath))
			#if MODS_ALLOWED
				currentTrackedSounds.set(gottenPath, FunkinFileSystem.getSound(gottenPath));
			#else
			{
				var folder:String = '';
				if(path == 'songs') folder = 'songs:';
		
				currentTrackedSounds.set(gottenPath, FunkinFileSystem.getSound(folder + getPath('$path/$key.$SOUND_EXT', SOUND, library)));
			}
			#end
		} catch (e:Dynamic) {
			if (ClientPrefs.isDebug())
				Sys.println('Paths.returnSound(): SOUND NOT FOUND: $key');
			CoolUtil.showPopUp('SOUND NOT FOUND: $key', 'Paths.returnSound():');
			return null;
		}
		localTrackedAssets.push(gottenPath);
		return currentTrackedSounds.get(gottenPath);
	}

	inline static public function fragShaderPath(key:String)
		return getPath('shaders/$key.frag');

	inline static public function vertShaderPath(key:String)
		return getPath('shaders/$key.vert');

	inline static public function fragShader(key:String)
		return getTextFromFile('shaders/$key.frag');

	inline static public function vertShader(key:String)
		return getTextFromFile('shaders/$key.vert');

	#if MODS_ALLOWED
	inline static public function mods(key:String = '') {
		return #if android StorageUtil.getExternalStorageDirectory() + #elseif mobile Sys.getCwd() + #end 'mods/' + key;
	}

	inline static public function modsFont(key:String) {
		return modFolders('fonts/' + key);
	}

	inline static public function modsJson(key:String) {
		return modFolders('data/' + key + '.json');
	}

	inline static public function modsVideo(key:String) {
		return modFolders('videos/' + key + '.' + VIDEO_EXT);
	}

	inline static public function modsSounds(path:String, key:String) {
		return modFolders(path + '/' + key + '.' + SOUND_EXT);
	}

	inline static public function modsImages(key:String, ?mod:String) {
		return modFolders('images/' + key + '.png', mod);
	}

	inline static public function modsXml(key:String) {
		return modFolders('images/' + key + '.xml');
	}

	inline static public function modsTxt(key:String) {
		return modFolders('images/' + key + '.txt');
	}

	/* Goes unused for now

	inline static public function modsShaderFragment(key:String, ?library:String)
	{
		return modFolders('shaders/'+key+'.frag');
	}
	inline static public function modsShaderVertex(key:String, ?library:String)
	{
		return modFolders('shaders/'+key+'.vert');
	}
	inline static public function modsAchievements(key:String) {
		return modFolders('achievements/' + key + '.json');
	}*/

	#if linux
	// Function for fuzzy finding on Linux, fixes issues where modpack developers are stupid
	// and are referring to files with the wrong case. Windows is lazy, but Unix is not so
	// it causes issues.
	// -GenieCMD
	static public function getFileLinux(path : String) {
		var fileName : String = Path.withoutDirectory(path);
		var dirToSearch : String = Path.directory(path);
		if (FileSystem.exists(dirToSearch)) {
			for (file in FileSystem.readDirectory(dirToSearch)) {
				var fullNewFilePath = Path.join([dirToSearch,file]);
				if (FileSystem.isDirectory(fullNewFilePath))
					continue;
				if (file.toLowerCase() == fileName.toLowerCase()) {
					return fullNewFilePath;
				}
			}
			return null;
		} else {
			return null;
		}
	}
	#end

	static public function modFolders(key:String, ?modDirectory:String) {
		modDirectory ??= Mods.currentModDirectory;

		if(modDirectory != null && modDirectory.length > 0) {
			var fileToCheck:String = mods(modDirectory + '/' + key);
			#if linux
			var actualFile = getFileLinux(fileToCheck);
			if (actualFile != null && FileSystem.exists(actualFile))
				return actualFile;
			#else
			if(FileSystem.exists(fileToCheck)) {
				return fileToCheck;
			}
			#end
		}

		for(mod in Mods.getGlobalMods()){
			var fileToCheck:String = mods(mod + '/' + key);
			#if linux
			var actualFile = getFileLinux(fileToCheck);
			if (actualFile != null && FileSystem.exists(actualFile))
				return actualFile;
			#else
			if(FileSystem.exists(fileToCheck)) {
				return fileToCheck;
			}
			#end
		}
		return #if android StorageUtil.getExternalStorageDirectory() + #elseif mobile Sys.getCwd() + #end 'mods/' + key;
	}
	#end

	public static function loadAnimateAtlas(spr:FlxAnimate, folderOrImg:Dynamic, spriteJson:Dynamic = null, animationJson:Dynamic = null) {
		var changedAnimJson = false;
		var changedAtlasJson = false;
		var changedImage = false;

		if (spriteJson != null) {
			changedAtlasJson = true;
			spriteJson = File.getContent(spriteJson);
		}

		if (animationJson != null) {
			changedAnimJson = true;
			animationJson = File.getContent(animationJson);
		}

		var frames:FlxAnimateFrames = new FlxAnimateFrames();

		// is folder or image path
		if (Std.isOfType(folderOrImg, String)) {
			var originalPath:String = folderOrImg;
			for (i in 0...10) {
				var st:String = '$i';
				if (i == 0)
					st = '';

				if (!changedAtlasJson) {
					spriteJson = getTextFromFile('images/$originalPath/spritemap$st.json');
					if (spriteJson != null) {
						// trace('found Sprite Json');
						changedImage = true;
						changedAtlasJson = true;
						loadSpriteMap(frames, spriteJson, folderOrImg = Paths.image('$originalPath/spritemap$st'));
						break;
					}
				}
				else if (Paths.fileExists('images/$originalPath/spritemap$st.png', IMAGE)) {
					// trace('found Sprite PNG');
					changedImage = true;
					loadSpriteMap(frames, spriteJson, folderOrImg = Paths.image('$originalPath/spritemap$st'));
					break;
				}
			}

			if (!changedImage) {
				// trace('Changing folderOrImg to FlxGraphic');
				changedImage = true;
				loadSpriteMap(frames, spriteJson, folderOrImg = Paths.image(originalPath));
			}

			if (!changedAnimJson) {
				// trace('found Animation Json');
				changedAnimJson = true;
				animationJson = getTextFromFile('images/$originalPath/Animation.json');
			}
		}

		// trace(folderOrImg);
		// trace(spriteJson);
		// trace(animationJson);

		spr.loadSeparateAtlas(animationJson, frames);
	}

	static function loadSpriteMap(frames:FlxAnimateFrames, spritemap:FlxSpriteMap, ?image:FlxGraphicAsset) {
		var spritemapFrames = FlxAnimateFrames.fromSpriteMap(spritemap, image);
		if (spritemapFrames != null)
			frames.addAtlas(spritemapFrames);
		return spritemapFrames;
	}

	public static var tempFramesCache:Map<String, FlxFramesCollection> = [];

	inline static public function getSparrowAtlasAlt(key:String)
		return FlxAtlasFrames.fromSparrow(image('$key.png', null, true, false, true), FunkinFileSystem.getText('$key.xml'));

	inline static public function getPackerAtlasAlt(key:String)
		return FlxAtlasFrames.fromSpriteSheetPacker(image('$key.png', null, true, false, true), FunkinFileSystem.getText('$key.txt'));

	inline static public function getAsepriteAtlasAlt(key:String)
		return FlxAtlasFrames.fromAseprite(image('$key.png', null, true, false, true), FunkinFileSystem.getText('$key.json'));

	static public function imageAlt(key:String, ?library:String, checkForAtlas:Bool = false, ?ext:String = "png") {
		if (checkForAtlas) {
			var atlasPath = getPath('images/$key/spritemap1.$ext', IMAGE, library, true);
			var multiplePath = getPath('images/$key/1.$ext', IMAGE, library, true);
			if (atlasPath != null && #if MODS_ALLOWED FunkinFileSystem.exists(atlasPath) #else OpenFlAssets.exists(atlasPath) #end)
				return atlasPath.substr(0, atlasPath.length - 14);
			if (multiplePath != null && #if MODS_ALLOWED FunkinFileSystem.exists(multiplePath) #else OpenFlAssets.exists(multiplePath) #end)
				return multiplePath.substr(0, multiplePath.length - 6);
		}
		return getPath('images/$key.$ext', IMAGE, library, true);
	}

	public static function getFrames(key:String, assetsPath:Bool = false, ?library:String, ?ext:String = null) {
		/* I think this brokes the character when song restarted
		if (tempFramesCache.exists(key)) {
			var frames = tempFramesCache[key];
			if (frames.parent != null && frames.parent.bitmap != null && frames.parent.bitmap.readable)
				return frames;
			else
				tempFramesCache.remove(key);
		}
		*/
		tempFramesCache[key] = loadFrames(key);
		return tempFramesCache[key];
	}

	static function loadFrames(path:String, Unique:Bool = false, Key:String = null, SkipAtlasCheck:Bool = false, SkipMultiCheck:Bool = false):FlxFramesCollection {
		var noExt = Path.withoutExtension(path);
		var atlasImage:Dynamic = null;

		if (!SkipMultiCheck && #if MODS_ALLOWED FunkinFileSystem.exists('$noExt/1.png') #else Assets.exists('$noExt/1.png') #end) {
			// MULTIPLE SPRITESHEETS!!

			var graphic = FlxG.bitmap.add("flixel/images/logo/default.png", false, '$noExt/mult');
			var frames = MultiFramesCollection.findFrame(graphic);
			if (frames != null)
				return frames;

			trace("no frames yet for multiple atlases!!");
			var cur = 1;
			var finalFrames = new MultiFramesCollection(graphic);
			trace("Final Frames: " + finalFrames);
			while(FunkinFileSystem.exists('$noExt/$cur.png')) {
				var spr = loadFrames('$noExt/$cur.png', false, null, false, true);
				trace("spr: " + spr);
				finalFrames.addFrames(spr);
				cur++;
			}
			return finalFrames;
		} else if (FunkinFileSystem.exists('$noExt.xml'))
			return getSparrowAtlasAlt(noExt);
		else if (FunkinFileSystem.exists('$noExt.txt'))
			return getPackerAtlasAlt(noExt);
		else if (FunkinFileSystem.exists('$noExt.json')) {
			var aSprite = getAsepriteAtlasAlt(noExt);
			return aSprite;
		}

		//var graph:FlxGraphic = FlxG.bitmap.add(path, Unique, Key);
		var graph:FlxGraphic = image(path, null, true, false, true); //use returnGraphic bc I want to use String instead of path (also, path one is buggy)
		if (graph == null)
			return null;
		return graph.imageFrame;
	}
}
