package backend;

import objects.Note;
import tjson.TJSON as Json;
import lime.utils.Assets;
import backend.Converters;

#if sys
import sys.io.File;
import sys.FileSystem;
#end

import backend.Section;

typedef StrumLine =
{
	var visible:Bool; //WIP Logic
	var position:String; //WIP Logic
	var characters:Array<String>;
	var cpu:Bool;
	var type:Int; //WIP Logic
}

typedef SwagSong =
{
	var song:String;
	var notes:Array<SwagSection>;
	var events:Array<Dynamic>;
	var bpm:Float;
	var needsVoices:Bool;
	var speed:Float;

	var player1:String;
	var player2:String;
	var gfVersion:String;
	var stage:String;

	@:optional var gameOverChar:String;
	@:optional var gameOverSound:String;
	@:optional var gameOverLoop:String;
	@:optional var gameOverEnd:String;
	
	@:optional var disableNoteRGB:Bool;

	@:optional var arrowSkin:String;
	@:optional var splashSkin:String;

	//MOD SPECIFIC
	@:optional var mania:Null<Int>;
	@:optional var keyCount:Null<Int>;
	@:optional var cutsceneType:String;

	//psych engine 1.0
	@:optional var format:String;

	//codename engine legacy (WIP)
	@:optional var strumLines:Array<StrumLine>;
}

class Song
{
	public var song:String;
	public var notes:Array<SwagSection>;
	public var events:Array<Dynamic>;
	public var bpm:Float;
	public var needsVoices:Bool = true;
	public var arrowSkin:String;
	public var splashSkin:String;
	public var gameOverChar:String;
	public var gameOverSound:String;
	public var gameOverLoop:String;
	public var gameOverEnd:String;
	public var disableNoteRGB:Bool = false;
	public var speed:Float = 1;
	public var stage:String;
	public var player1:String = 'bf';
	public var player2:String = 'dad';
	public var gfVersion:String = 'gf';

	private static function onLoadJson(songJson:Dynamic) // Convert old charts to newest format, or convert new format to old format?
	{
		if(songJson.format == null)
			throw new haxe.Exception('No chart format found!');

		if (ClientPrefs.isDebug())
			trace('Loaded ${songJson.format} Song!');

		if(songJson.gfVersion == null)
		{
			songJson.gfVersion = songJson.player3;
			songJson.player3 = null;
		}

		if(StringTools.startsWith(songJson.format, 'psych_v1')) {
			songJson.format = 'psych_v1';

			var characters:Array<String> = [songJson.player1, songJson.player2, songJson.gfVersion];
			for (i in 0...characters.length)
			{
				switch(characters[i])
				{
					case 'pico-playable':
						characters[i] = 'pico-player';

					case 'tankman-playable':
						characters[i] = 'tankman-player';
				}
			}

			songJson.player1 = characters[0];
			songJson.player2 = characters[1];
			songJson.gfVersion = characters[2];
		}

		if(songJson.events == null && songJson.format == 'psych_legacy')
		{
			songJson.events = [];
			for (secNum in 0...songJson.notes.length)
			{
				var sec:SwagSection = songJson.notes[secNum];

				var i:Int = 0;
				var notes:Array<Dynamic> = sec.sectionNotes;
				var len:Int = notes.length;
				while(i < len) {
					var note:Array<Dynamic> = notes[i];
					// if notedata is -1 (event note)
					if(note[1] < 0) {
						songJson.events.push([note[0], [[note[2], note[3], note[4]]]]);
						notes.remove(note);
						len = notes.length;
						continue;
					}
					i++;
				}
			}
		}
	}

	public function new(song, notes, bpm)
	{
		this.song = song;
		this.notes = notes;
		this.bpm = bpm;
	}

	public static function loadRawSong(jsonInput:String, ?folder:String):String {
		try {
			var isEvent:Bool = jsonInput.startsWith('events');
			var lastDashIndex = jsonInput.lastIndexOf('-');
			var difficulty = jsonInput.substring(lastDashIndex + 1);
			var songName = isEvent ? PlayState.SONG.song : jsonInput.substring(0, lastDashIndex);
			var chartsFolder:String = isEvent ? 'events' : 'charts/${difficulty}';

			if (Paths.formatToSongPath(difficulty) == Paths.formatToSongPath(Difficulty.defaultDifficulty))
				difficulty = Difficulty.defaultDifficulty; 

			var formattedFolder:String = Paths.formatToSongPath(folder);
			var formattedSong:String = Paths.formatToSongPath(jsonInput);
			var rawJson:String = null;

			#if MODS_ALLOWED
			var modSongPath = Paths.modsJson('$formattedFolder/$formattedSong');
			var modCneChartPath = Paths.modFolders('songs/$songName/$chartsFolder.json');
			var modCneMetaPath = Paths.modFolders('songs/$songName/meta-$difficulty.json');

			if (!FunkinFileSystem.exists(modCneMetaPath))
				modCneMetaPath = Paths.modFolders('songs/$songName/meta.json');

			trace(modCneMetaPath);


			if (FunkinFileSystem.exists(modCneChartPath)) {
				var chartData = Json.parse(FunkinFileSystem.getText(modCneChartPath).trim());
				var metaData = Json.parse(FunkinFileSystem.getText(modCneMetaPath).trim());
				rawJson = Converters.parseCodenameChart(chartData, metaData, isEvent);
			} else if (FunkinFileSystem.exists(modSongPath)) {
				rawJson = FunkinFileSystem.getText(modSongPath).trim();
			}
			#end

			if (rawJson == null) {
				var baseSongPath = Paths.json('$formattedFolder/$formattedSong');
				var baseCneChartPath = Paths.getPath('songs/$songName/$chartsFolder.json', TEXT, null, true);
				var baseCneMetaPath = Paths.getPath('songs/$songName/meta-$difficulty.json', TEXT, null, true);

				if (!FunkinFileSystem.exists(baseCneMetaPath))
					baseCneMetaPath = Paths.getPath('songs/$songName/meta.json', TEXT, null, true);

				if (FunkinFileSystem.exists(baseCneChartPath)) {
					var chartData = Json.parse(FunkinFileSystem.getText(baseCneChartPath));
					var metaData = Json.parse(FunkinFileSystem.getText(baseCneMetaPath));
					rawJson = Converters.parseCodenameChart(chartData, metaData, isEvent);
				} else if (FunkinFileSystem.exists(baseSongPath)) {
					rawJson = FunkinFileSystem.getText(baseSongPath);
				}

				if (rawJson == null)
					throw new haxe.Exception('Missing file: $baseSongPath');

				rawJson = rawJson.trim();
			}

			while (rawJson != null && !rawJson.endsWith("}")) {
				rawJson = rawJson.substr(0, rawJson.length - 1);
			}

			return rawJson;

		} catch(e:Dynamic) { 
			trace('Error loading raw song: $e');
			//this should fix no data problem.
			return "
				{
				  'events': [],
				  'song': '',
				  'notes': [],
				  'bpm': 0,
				  'needsVoices': true,
				  'speed': 1,
				  'player1': '',
				  'player2': '',
				  'gfVersion': '',
				  'stage': '',
				  'format': 'psych_legacy'
				}
			";
		}
	}

	public static function loadFromJson(jsonInput:String, ?folder:String):SwagSong
	{
		// FIX THE CASTING ON WINDOWS/NATIVE
		// Windows???
		// trace(songData);

		// trace('LOADED FROM JSON: ' + songData.notes);
		/* 
			for (i in 0...songData.notes.length)
			{
				trace('LOADED FROM JSON: ' + songData.notes[i].sectionNotes);
				// songData.notes[i].sectionNotes = songData.notes[i].sectionNotes
			}

				daNotes = songData.notes;
				daSong = songData.song;
				daBpm = songData.bpm; */

		return parseRawJSON(jsonInput, loadRawSong(jsonInput, folder));
	}

	public static function parseRawJSON(jsonInput:String, rawSONG:String) {
		var songJson:Dynamic = parseJSONshit(rawSONG);
		if(!jsonInput.startsWith('events')) StageData.loadDirectory(songJson);
		onLoadJson(songJson);
		return songJson;
	}

	public static function parseJSONshit(rawJson:String):SwagSong
	{
		var parsed:Dynamic = Json.parse(rawJson);
		
		if (parsed.song != null) {
			if (Std.isOfType(parsed.song, String)) {
				parsed.format ??= 'psych_v1';
				return parsed;
			}
			
			parsed.song.format ??= 'psych_legacy';
			return parsed.song;
		}
		
		if (parsed.events != null) {
			return {
				events: cast parsed.events,
				song: "",
				notes: [],
				bpm: 0,
				needsVoices: true,
				speed: 1,
				player1: "",
				player2: "",
				gfVersion: "",
				stage: "",
				format: 'psych_legacy'
			};
		}

		throw new haxe.Exception("No song data found, or is invalid.");
	}

	public static function updateManiaKeys(songData:SwagSong, ?noUpdate:Bool = false):Int {
		if (songData == null)
			return Note.maniaKeys = 4;
		
		var keys = null;

		if (songData.mania != null)
			if ((songData.format ?? '').startsWith('psych_v1') || (songData.splashSkin != null) || songData.cutsceneType != null) {
				keys = songData.mania + 1;
			}
			else {
				switch (songData.mania) {
					case 0: // 4k
						keys = 4;
					case 4: // 5k
						keys = 5;
					case 1, 5, 6: // 6k
						keys = 6;
					case 2, 7: // 7k
						keys = 7;
					case 3, 8: // 9k
						keys = 9;
					default:
						keys = songData.mania;
				}
			}

		if (keys == null && songData.keyCount != null)
			keys = songData.keyCount;

		if (noUpdate)
			return keys ?? 4;

		return Note.maniaKeys = keys ?? 4;
	}
}